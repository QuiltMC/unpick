plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    group = 'org.quiltmc.unpick'

    def ENV = System.getenv()
    version = "3.0.1" + (ENV.SNAPSHOTS_URL ? "-SNAPSHOT" : "")
    version = version + (ENV.GITHUB_ACTIONS ? "" : "+local")

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        api libs.asm
        api libs.asm.tree
        implementation libs.asm.commons
        implementation libs.asm.util

        // Use JUnit test framework
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.+'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"

        if (JavaVersion.current().isJava9Compatible()) {
            it.options.release = 8
        }
    }

    java {
        withSourcesJar()
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        repositories {
            if (ENV.MAVEN_URL) {
                repositories.maven {
                    name "Quilt"
                    url ENV.MAVEN_URL
                    credentials {
                        username ENV.MAVEN_USERNAME
                        password ENV.MAVEN_PASSWORD
                    }
                }
            }
            if (ENV.SNAPSHOTS_URL) {
                repositories.maven {
                    name "QuiltSnapshot"
                    url ENV.SNAPSHOTS_URL
                    credentials {
                        username ENV.SNAPSHOTS_USERNAME
                        password ENV.SNAPSHOTS_PASSWORD
                    }
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(javadocJar)
            }
        }
    }
}

dependencies {
    implementation project(':unpick-format-utils')
}
